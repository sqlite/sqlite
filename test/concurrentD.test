# 2026 February 18
#
# The author disclaims copyright to this source code.  In place of
# a legal notice, here is a blessing:
#
#    May you do good and not evil.
#    May you find forgiveness for yourself and forgive others.
#    May you share freely, never taking more than you give.
#
#*************************************************************************
# This file implements regression tests for SQLite library.
#
# More specifically, it implements tests for BEGIN CONCURRENT with wal2
# databases.
#

set testdir [file dirname $argv0]
source $testdir/tester.tcl
set testprefix concurrentD

do_execsql_test 1.0 {
  PRAGMA journal_mode = wal2;
  PRAGMA foreign_keys = on;
  CREATE TABLE p1(a PRIMARY KEY, b TEXT UNIQUE) WITHOUT ROWID;
  CREATE TABLE c1(
    x PRIMARY KEY, 
    y TEXT REFERENCES p1(b) DEFERRABLE INITIALLY DEFERRED 
  ) WITHOUT ROWID;
  CREATE INDEX c1b ON c1(y);

  WITH s(i) AS (
    SELECT 1 UNION ALL SELECT i+1 FROM s WHERE i<20
  ) 
  INSERT INTO p1 SELECT i, randomblob(16) FROM s;
  INSERT INTO c1 SELECT a, b FROM p1;

} {wal2}

expr srand(5)

proc randomint {min max} {
  set ret [expr {$min + int(abs(rand()) * ($max-$min))}]
  return $ret
}

set DBLIST [list db1 db2 db3 db4]

set T {
  UPDATE c1 SET y = (
    SELECT b FROM p1 WHERE a = randomint(1, 20)
  ) WHERE x = randomint(1, 20);
  UPDATE p1 SET b = randomblob(16) WHERE a=randomint(1,25);
}

foreach hdl $DBLIST {
  sqlite3 $hdl test.db
  $hdl func randomint randomint
  $hdl eval {
    PRAGMA foreign_keys = ON
  }
}

for {set i 0} {$i < 2000} {incr i} {
  foreach hdl $DBLIST {
    if {$i==1000} {
      $hdl eval "PRAGMA journal_size_limit=8192;"
    }
    $hdl eval "BEGIN CONCURRENT"
    $hdl eval $T
  }

  set tag ""
  foreach hdl $DBLIST {
    if {[catch {$hdl eval COMMIT }]} {
      $hdl eval ROLLBACK
      append tag R
    } else {
      append tag C
    }
    if {$i>1000 && ($i % 5)==0} {
      $hdl eval "PRAGMA wal_checkpoint"
    }
  }

  do_execsql_test 1.1.$i.$tag {
    PRAGMA foreign_key_check;
    PRAGMA integrity_check;
  } {ok}

  if {[set_test_counter errors]} break
}

foreach hdl $DBLIST {
  $hdl close
}

#-------------------------------------------------------------------------
reset_db
do_execsql_test 2.0 {
  PRAGMA journal_mode = wal2;
  PRAGMA journal_size_limit = 8192;
  CREATE TABLE t1(a INTEGER PRIMARY KEY, b UNIQUE);
  CREATE TABLE t2(a INTEGER PRIMARY KEY, b UNIQUE);
  CREATE TABLE t3(a INTEGER PRIMARY KEY, b UNIQUE);
} {wal2 8192}

do_test 2.1.1 { expr [file size test.db-wal2]>1000 } {0}
do_execsql_test 10.1.2 {
  INSERT INTO t1 VALUES(0, 'xyz');
  PRAGMA wal_checkpoint;
} {0 11 9}

do_test 2.1.3 { expr [file size test.db-wal]>1000  } {1}
do_test 2.1.4 { expr [file size test.db-wal2]>1000 } {1}

sqlite3 db1 test.db
sqlite3 db2 test.db
sqlite3 db3 test.db

db1 eval "PRAGMA journal_size_limit=8192;"
db2 eval "PRAGMA journal_size_limit=8192;"
db3 eval "PRAGMA journal_size_limit=8192;"

proc random {} {
  expr int(rand()*(2*1024*1024*1024*1024))
}
db func random random
db1 func random random
db2 func random random
db3 func random random

for {set i 0} {$i < 1000} {incr i} {
  do_test 2.2.$i {
    execsql {
      BEGIN CONCURRENT;
        INSERT INTO t3 VALUES( random(), randomblob(100) );
    }
    execsql {
      BEGIN CONCURRENT;
        INSERT INTO t1 VALUES( random(), randomblob(100) );
      COMMIT;
      PRAGMA wal_checkpoint;
    } db1
    execsql {
      BEGIN CONCURRENT;
        INSERT INTO t2 VALUES( random(), randomblob(100) );
      COMMIT;
      PRAGMA wal_checkpoint;
    } db2
    execsql {
      BEGIN CONCURRENT;
        INSERT INTO t3 VALUES( random(), randomblob(100) );
      COMMIT;
      PRAGMA wal_checkpoint;
    } db3
    execsql COMMIT
  } {}
}

db1 close
db2 close
db3 close


finish_test


